/*
 * Copyright (c) 2014 Jonas Kalderstam.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.14.0'
        // the latest version of the android-apt plugin for annotations
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.4+'
    }
}

apply plugin: 'com.android.application'

// For annotations
apply plugin: 'android-apt'
def AAVersion = '3.0.1'

repositories {
    mavenCentral()
    mavenLocal()
}

// Version number
def versionMajor = 5 // Major UI overhauls
def versionMinor = 5 // Some new functionality
def versionPatch = 3 // Bug fixes
def versionBuild = 0 // Bump for dogfood builds, public betas, etc.

// Version name from git
def getVersionName = { ->
  try {
  def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags'
        standardOutput = stdout
    }
    return stdout.toString().trim()
  } catch (Exception e) {
    return "GITFAILEDINGRADLE"
  }
}

android {
    compileSdkVersion 19
    buildToolsVersion "19.1.0"

    lintOptions {
        abortOnError false
    }

    defaultConfig {
      applicationId "com.nononsenseapps.notepad"
      minSdkVersion 14
      targetSdkVersion 19
      versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild * 10
      versionName getVersionName()

        // If Non-Free libraries are to be used
        buildConfigField "boolean", "NONFREE", "false"
        buildConfigField "boolean", "DROPBOX_ENABLED", "false"
    }

    productFlavors {
        free {}
    }

    if (project.hasProperty('STORE_FILE')) {
      signingConfigs {
        release {
          storeFile file(STORE_FILE)
          storePassword STORE_PASSWORD
          keyAlias KEY_ALIAS
          keyPassword KEY_PASSWORD
        }
      }

      buildTypes {
        release {
          signingConfig signingConfigs.release
        }
      }
    } else {
      println "No key store defined. Signed release not available..."
    }
}

// annotations
apt {
    arguments {
        resourcePackageName android.defaultConfig.applicationId
        androidManifestFile variant.processResources.manifestFile
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: '*.jar')
    compile 'com.android.support:support-v4:19.0.1+'
    compile 'joda-time:joda-time:2.3'
    // Dashclock
    compile 'com.google.android.apps.dashclock:dashclock-api:2.0.0'
    // FilePicker
    compile 'com.nononsenseapps:filepicker:1.1.3+'
    compile project(':external:ActionBar-PullToRefresh')
    compile project(':external:datetimepicker')
    compile project(':external:drag-sort-listview')
    compile project(':external:ShowcaseView')
    // annotations
    apt "org.androidannotations:androidannotations:$AAVersion"
    compile "org.androidannotations:androidannotations-api:$AAVersion"
}